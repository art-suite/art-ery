import &StandardImport

###
  A) Populate context.dataUpdates
  B) if Neptune.Art.Flux is defined, and this is the root request or resposne
     Perform 'local updates'
  TODO:
    Eventually we will want a way to say that some record updates should not be returned client-side.
    First pass
      - data has already gone through the after-pipeline, so any after-filters can removed fields
        the current user can't see. TODO: create privacy filters
      - if data is empty, then don't add it to updates. Nothing to add anyway. DONE

class DataUpdatesFilter extends &Filter

  updateRequestTypeRegExp = /^(create|update)/
  deleteRequestTypeRegExp = /^delete/

  # for subrequests, this will still be on the server
  # for root requests, there is work to do on both the client and server
  @location :both
  @group :outer

  updateDataUpdatesContext = (response, context)->
    response extract key, type, responseData, pipelineName

    context ?= response.context

    if groupName = switch
        when updateRequestTypeRegExp.test type then :dataUpdates
        when deleteRequestTypeRegExp.test type then :dataDeletes

      # if groupName == :dataUpdates
      #   log updateDataUpdatesContext: {} pipelineName, type, key, responseData

      switch
      when responseData is Array
        each record in responseData
          key = response.pipeline.toKeyString record
          # log "" vivifyObjectPathAndSet(A) #{groupName}, #{pipelineName}, #{key} (#{type})
          vivifyObjectPathAndSet context, groupName, pipelineName, key, record

      when key, response.pipeline.isRecord responseData
        responseData ?=
          response.pipeline.toKeyObject? key || responseData
          || {}

        key ?= response.pipeline.toKeyString responseData
        # log "" vivifyObjectPathAndSet(B) #{groupName}, #{pipelineName}, #{key} (#{type})
        vivifyObjectPathAndSet context, groupName, pipelineName, key, responseData

    context

  @after all: (response) ->
    if response.isRootRequest

      if response.location != :client
            response.withMergedPropsWithoutNulls {dataUpdates, dataDeletes} = response.context
      else  response
      .tap (response) -> @applyFluxUpdates response if response.location != :server && @models

    else
      # log DataUpdatesFilter3: {} response
      updateDataUpdatesContext response
      response

  applyFluxUpdates: (response) ->
    response.responseProps extract dataUpdates, dataDeletes

    @applyFluxDataUpdates dataUpdates
    @applyFluxDataDeletes dataDeletes

    if updateRequestTypeRegExp.test response.type
      @applyFluxDataUpdates updateDataUpdatesContext(response, {}).dataUpdates
    if deleteRequestTypeRegExp.test response.type
      @applyFluxDataDeletes updateDataUpdatesContext(response, {}).dataDeletes

  @getter
    models: -> Neptune.Art.Flux?.models

  applyFluxDataUpdates: (dataUpdates) ->
    each dataUpdatesByKey, pipelineName in dataUpdates when @models[pipelineName]?.dataUpdated is Function
      each data, key in dataUpdatesByKey
        @models[pipelineName].dataUpdated key, data

  applyFluxDataDeletes: (dataDeletes) ->
    each dataDeletesByKey, pipelineName in dataDeletes when @models[pipelineName]?.dataDeleted is Function
      each data, key in dataDeletesByKey
        @models[pipelineName].dataDeleted key, data
